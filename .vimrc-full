" Miscellaneous
let g:python_host_prog  = '/usr/bin/python2'
let g:python3_host_prog = '/usr/bin/python3'
set updatetime=300
set colorcolumn=80
set title nocompatible nowritebackup nobackup
set mouse=a undofile undolevels=1000 undodir=$HOME/.vim/undo
set directory=$HOME/.vim/swap
set path+=** wildmenu
set hidden backspace=indent,eol,start
set splitright splitbelow
set autoindent smartindent copyindent smarttab expandtab
set shiftwidth=4 tabstop=4 softtabstop=4
set hlsearch incsearch ignorecase smartcase
set nowrap number nomodeline ttyfast lazyredraw
set completeopt=noinsert,noselect,menu
filetype off
call plug#begin('~/.vim/plugged')
" utilities
Plug 'junegunn/fzf.vim'
Plug 'junegunn/fzf', { 'dir': '~/.local/bin/fzf', 'do': './install --all' }
Plug 'ap/vim-buftabline'
" Git
Plug 'mhinz/vim-signify'
" Lang Packs
Plug 'sheerun/vim-polyglot', { 'do': './build' }
" Aestetics
Plug 'acarapetis/vim-colors-github'
Plug 'tomasiser/vim-code-dark'
" LSP
Plug 'autozimu/languageclient-neovim', { 'branch': 'next', 'do': 'bash install.sh', }
" Deoplete + Dependencies
Plug 'shougo/deoplete.nvim'
Plug 'deoplete-plugins/deoplete-tag'
Plug 'roxma/nvim-yarp'
Plug 'roxma/vim-hug-neovim-rpc'
 call plug#end()

call plug#end()
filetype plugin indent on
syntax on
" tabline
let g:buftabline_indicators = 1
let g:buftabline_separators = 1
let g:buftabline_plug_max   = 0
" Git
let g:signify_sign_add               = '>'
let g:signify_sign_delete            = '<'
let g:signify_sign_change            = '~'
" Langs
let g:ansible_attribute_highlight       = 'ab'
let g:ansible_extra_keywords_highlight  = 1
let g:ansible_name_highlight            = 'd'
let g:ansible_normal_keywords_highlight = 'Define'
let g:ansible_yamlKeyName               = 'yamlKey'
let g:go_highlight_array_whitespace_error   = 1
let g:go_highlight_build_constraints        = 1
let g:go_highlight_chan_whitespace_error    = 1
let g:go_highlight_extra_types      = 1
let g:go_highlight_fields           = 1
let g:go_highlight_function_calls   = 1
let g:go_highlight_function_parameters  = 1
let g:go_highlight_functions        = 1
let g:go_highlight_generate_tags    = 1
let g:go_highlight_operators        = 1
let g:go_highlight_space_tab_error  = 1
let g:go_highlight_trailing_whitespace_error = 1
let g:go_highlight_types                 = 1
let g:go_highlight_variable_assignments  = 1
let g:go_highlight_variable_declarations = 1
let g:java_highlight_all    = 1
let g:python_highlight_all  = 1
" FZF fuzzy
let g:fzf_action = {'alt-enter':'vsplit' }
let g:fzf_files_options = "--preview 'if file {1} | grep -Ei \"text|JSON\"; then cat {1} ; fi'"
"""     Shortcuts   "
" do last action on a visual selection
vnoremap . :'<,'>:normal .<CR>
" navigate tabs Tab (fw) S-Tab (prev)
nnoremap <Tab>   :<C-u>bn!<CR>
nnoremap <S-Tab> :<C-u>bp!<CR>
" C-c close buffer
nnoremap <C-c> :<C-u>bp<BAR>sp<BAR>bn<BAR>bd<CR>
" Leader map
let mapleader = ' '
" leader y/p to use system clipboard, C-c in vmode for xclip
nnoremap <leader>y "+y
nnoremap <leader>p "+p
vnoremap <C-c> :'<,'>w !xclip -sel clip<CR><CR>
" " Resize split window horizontally and vertically
noremap <S-M-Down>  :<C-u>2winc-<CR>
noremap <S-M-Left>  :<C-u>2winc<<CR>
noremap <S-M-Right> :<C-u>2winc><CR>
noremap <S-M-Up>    :<C-u>2winc+<CR>
" Utility shortcuts with leader:
nnoremap <leader><Tab>  :<C-u>Buffers<CR>
nnoremap <leader>b      :<C-u>Files<CR>
nnoremap <leader>f      :<C-u>Rg<CR>
nnoremap <leader>t      :<C-u>Tags<CR>
" Utility for Markdown and Ansible
nnoremap <leader>j  :<C-u>set ft=
" Theming
set noshowmode noshowcmd laststatus=0 ruler   " hide statusline
set rulerformat=%20(%m%r%w\ %y\ %l/%c%)\        " Modified+FileType+Ruler
set background=dark
set termguicolors
augroup customsyntax
    autocmd! customsyntax
    " Remove trailing whitespaces and lines
    autocmd BufWritePre * silent! :call StripTrailingWhiteSpace()
    " Refresh tags on save
    autocmd BufWritePost * silent! :call GenTags()
    " Custom syntax highlight
    autocmd Syntax,InsertEnter * syntax match myFunction /\<\k\+\ze(/
    autocmd Syntax,InsertEnter * syntax match myDeclaration_1 /\<\k\+\ze\s*=[a-zA-Z0-9 $:.\/\\]/
    autocmd Syntax,InsertEnter * syntax match myDeclaration_2 /\<.*\k\+\ze\s*:=[a-zA-Z0-9 $:.\/\\]/
augroup end
highlight link myFunction       Function
highlight link myDeclaration_1  Identifier
highlight link myDeclaration_2  Identifier
colorscheme codedark
highlight Normal        guibg=#101010
highlight TabLineSel    guifg=#262626 guibg=#8a8a8a
" FUNCTIONS --------------------------------------------------------------------
" Toggle Theme
map <silent> <C-e> :<C-u>call ToggleTheme()<CR>
function! ToggleTheme()
    if &background == 'light'
        set background=dark
        colorscheme codedark
        highlight Normal        guibg=#101010
        highlight TabLineSel    guifg=#262626 guibg=#8a8a8a
    else
        set background=light
        colorscheme github
        highlight Normal        guibg=#FFFFFF
    endif
endfunction
" Lint the entire project using filetype as reference. out to quickfix
function! LintProject()
    silent!
    cgete system('lint-project ' . &filetype . " .")
    :vert copen
endfun
" Generate tags
function! GenTags()
    if isdirectory(".git") || filereadable(".project")
        silent!
        exec "!rm -f ./tags; ctags -R ."
        redraw!
    endif
endfun
" Remove Trailing Spaces and empty lines
function! StripTrailingWhiteSpace()
    " don't strip on these filetypes
    if &ft =~ 'markdown'
        return
    endif
    %s/\s\+$//e
    %s/\($\n\s*\)\+\%$//e
    redraw!
endfun
" Default IDE-Style keybindings EDMRL, errors, definition, references, rename, indent
nnoremap <leader>A  :<C-u>call LintProject()<CR>
nnoremap <leader>e  :<C-u>vert copen<BAR>vert resize 80<CR>
nnoremap <leader>d  :<C-u>vert stag <c-r>=expand("<cword>")<CR><CR>
nnoremap <leader>m  :<C-u>Rg <c-r>=expand("<cword>")<CR><CR>
nnoremap <leader>r  :<C-u>!grep -rl <c-r>=expand("<cword>")<CR><BAR>xargs sed -i 's/<c-r>=expand("<cword>")<CR>//g'<Left><Left><Left>
nnoremap <leader>l  :<C-u>mkview<CR>ggVG=:<C-u>loadview<CR>
" LSP SETUP --------------------------------------------------------------------
augroup lspbindings
    autocmd! lspbindings
    " IDE-like keybindings
    autocmd Filetype c,cpp,python,go nnoremap <buffer> K  :<C-u>call LanguageClient#textDocument_hover()<CR>
    " Default IDE-Style keybindings EDMRL, errors, definition, references, rename, indent
    autocmd Filetype c,cpp,python,go nnoremap <buffer> <leader>d :<C-u>call LanguageClient#textDocument_definition({'gotoCmd': 'split'})<CR>
    autocmd Filetype c,cpp,python,go nnoremap <buffer> <leader>l :<C-u>call LanguageClient#textDocument_formatting()<CR>
    autocmd Filetype c,cpp,python,go nnoremap <buffer> <leader>m :<C-u>call LanguageClient#textDocument_references()<CR>
    autocmd Filetype c,cpp,python,go nnoremap <buffer> <leader>r :<C-u>call LanguageClient#textDocument_rename()<CR>
    autocmd Filetype c,cpp,python,go nnoremap <buffer> <leader>h :<C-u>call LanguageClient#textDocument_codeAction()<CR>
augroup end
" Async enable Deoplete for better performances
autocmd InsertEnter * call deoplete#enable()
let g:deoplete#enable_at_startup = 0                " Start on insert mode
" LSP Language Client
let g:LanguageClient_autoStart  = 1
let g:LanguageClient_diagnosticsEnable  = 1
let g:LanguageClient_diagnosticsList = "Quickfix"
let g:LanguageClient_serverCommands     = {
            \ 'c': ['clangd'],
            \ 'cpp': ['clangd'],
            \ 'go': ['gopls'],
            \ 'python': ['pyls'],
            \}
